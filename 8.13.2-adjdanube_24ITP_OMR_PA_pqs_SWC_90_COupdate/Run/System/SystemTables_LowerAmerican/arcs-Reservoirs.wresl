!***************************************************************************************************************************************
!<license>
!    Copyright (C) 2017, 2018 State of California, Department of Water Resources. This file is part of the CalSim 3.

!    The CalSim 3 is free software: !<license>

!    you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free
!    Software Foundation, either version 3 of the License, or(at your option) any later version.

!    CalSim 3 is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty
!    of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.

!    You should have received a copy of the GNU General Public License along with CalSim 3.  If not, see <http://www.gnu.org/licenses>.
!</license>
!****************************************************************************************************************************************

/*
File Name: arcs-reservoirs.wresl
CalSim 3.0 System File

Purpose:


*/

!Folsom Lake
define S_FOLSMlevel1 {value 90}
define S_FOLSM_1     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_FOLSMlevel2 {timeseries kind 'STORAGE-LEVEL' units 'TAF'}
define S_FOLSM_2     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_FOLSM_3     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_FOLSMlevel4 {timeseries kind 'STORAGE-LEVEL' units 'TAF'}
define S_FOLSM_4     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_FOLSMlevel5 {timeseries kind 'STORAGE-LEVEL' units 'TAF'}
define S_FOLSM_5     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_FOLSMlevel6 {value 967.}
define S_FOLSM_6     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_FOLSM 	     {std kind 'STORAGE' units 'TAF'}

!For Water Forum calcs (watforum_mitigation_oasisval.wresl)

!Rest of Folsom storage zones defined in FolRuleCurv.wresl
goal S_FOLSMZone1 {S_FOLSM_1 < S_FOLSMlevel1}
goal S_FOLSMZone2 {S_FOLSM_2 < S_FOLSMlevel2-S_FOLSMlevel1}
goal storageFOLSM {S_FOLSM=S_FOLSM_1+S_FOLSM_2+S_FOLSM_3+S_FOLSM_4+S_FOLSM_5+S_FOLSM_6}

define F_FOLSM  {std kind 'FLOW-SPILL-NON-RECOV' units 'CFS'}
define E_FOLSM  {lower unbounded kind 'EVAPORATION' units 'CFS'}
define A_FOLSM  {std kind 'SURFACE-AREA' units 'ACRES'}
define ER_FOLSM {timeseries kind 'EVAPORATION-RATE' units 'INCHES'}
define A_FOLSMlast    {select area from American_ResInfo given storage=1000*S_FOLSM(-1) use linear where ResNum=7}
define A_FOLSMforward {select area from American_ResInfo given storage=1050*S_FOLSM(-1) use linear where ResNum=7}
define A_FOLSMback    {select area from American_ResInfo given storage=950*S_FOLSM(-1) use linear where ResNum=7}
define coefev_FOLSM   {value (A_FOLSMforward - A_FOLSMback)/(100*max(0.01,S_FOLSM(-1)))}
goal area_FOLSM {A_FOLSM=A_FOLSMlast+1000*coefev_FOLSM*S_FOLSM-1000*coefev_FOLSM*S_FOLSM(-1)}
goal evapFOLSM {E_FOLSM*cfs_af=(ER_FOLSM/24)*A_FOLSMlast+(ER_FOLSM/24)*A_FOLSM}
define relcapFOLSM {select discharge from American_ResInfo given storage=1000*S_FOLSM(-1) use linear where ResNum=7}
goal maxreleasE_FOLSM {C_FOLSM<relcapFOLSM}

!Lake Natomas
define S_NTOMAlevel1 {value 1.75}
define S_NTOMA_1 {std kind 'STORAGE-ZONE' units 'TAF'}
goal S_NTOMAZone1 {S_NTOMA_1 < S_NTOMAlevel1}
define S_NTOMAlevel2 {value 6.5}
define S_NTOMA_2 {std kind 'STORAGE-ZONE' units 'TAF'}
goal S_NTOMAZone2 {S_NTOMA_2 < S_NTOMAlevel2-S_NTOMAlevel1}
define S_NTOMAlevel3 {value 6.5}
define S_NTOMA_3 {std kind 'STORAGE-ZONE' units 'TAF'}
goal S_NTOMAZone3 {S_NTOMA_3 < S_NTOMAlevel3-S_NTOMAlevel2}
define S_NTOMAlevel4 {value 8.0}
define S_NTOMA_4 {std kind 'STORAGE-ZONE' units 'TAF'}
goal S_NTOMAZone4 {S_NTOMA_4 < S_NTOMAlevel4-S_NTOMAlevel3}
define S_NTOMAlevel5 {value 8.0}
define S_NTOMA_5 {std kind 'STORAGE-ZONE' units 'TAF'}
goal S_NTOMAZone5 {S_NTOMA_5 < S_NTOMAlevel5-S_NTOMAlevel4}
define S_NTOMA {std kind 'STORAGE' units 'TAF'} !Lake Natoma
goal storageNTOMA {S_NTOMA=S_NTOMA_1+S_NTOMA_2+S_NTOMA_3+S_NTOMA_4+S_NTOMA_5}
define F_NTOMA {std kind 'FLOW-SPILL-NON-RECOV' units 'CFS'}
define E_NTOMA {lower unbounded kind 'EVAPORATION' units 'CFS'}
define A_NTOMA {std kind 'SURFACE-AREA' units 'ACRES'}
define ER_NTOMA {timeseries kind 'EVAPORATION-RATE' units 'INCHES'}
define A_NTOMAlast {select area from American_ResInfo given storage=1000*S_NTOMA(-1) use linear where ResNum=8}
define A_NTOMAforward {select area from American_ResInfo given storage=1050*S_NTOMA(-1) use linear where ResNum=8}
define A_NTOMAback {select area from American_ResInfo given storage=950*S_NTOMA(-1) use linear where ResNum=8}
define coefev_NTOMA {value (A_NTOMAforward - A_NTOMAback)/(100*max(0.01,S_NTOMA(-1)))}
goal areA_NTOMA {A_NTOMA=A_NTOMAlast+1000*coefev_NTOMA*S_NTOMA-1000*coefev_NTOMA*S_NTOMA(-1)}
goal evapNTOMA {E_NTOMA*cfs_af=(ER_NTOMA/24)*A_NTOMAlast+(ER_NTOMA/24)*A_NTOMA}
define relcapNTOMA {select discharge from American_ResInfo given storage=1000*S_NTOMA(-1) use linear where ResNum=8}
goal maxreleaseNTOMA {C_NTOMA<relcapNTOMA}
