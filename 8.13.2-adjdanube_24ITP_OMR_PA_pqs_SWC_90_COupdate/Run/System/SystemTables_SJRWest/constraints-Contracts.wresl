/*
LIMIT DIVERSIONS TO CONTRACT-LIMIT AMOUNTS AND CHANNEL CAPACITY CONSTRAINTS

NAMING TAKES THE FOLLOWING FORMS:
CL_{ORIGIN PREFIX}_{DESTINATION}    - limitation on specific contractors extraction from general source



Original formulation: C. Olson (MWH) June 2007
*/



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! WBA 50      !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! City of Tracy
goal limit_D_WTPJJO_50_PU  {D_WTPJJO_50_PU < CLM_50_PUa * taf_cfs * perdel_cvpmi_s 
                                           + CLM_50_PUc * taf_cfs * perdel_cvpag_s}
goal limit_D_WTPDGT_50_PU  {D_WTPDGT_50_PU < CLM_50_PUb * taf_cfs}

! Banta Carbona & Westside
goal limit_D_50_PA1        {D_DMC021_50_PA1 < CLM_50_PA1 * taf_cfs * perdel_cvpag_s }
!***************************************************
goal limit_D_SJR062_50_PA1 {D_SJR062_50_PA1 = 0.} ! Diversions from the San Joaquin River, including recapture, need to be investigated
!***************************************************


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! WBA 71      !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Musco Olive Products
goal limit_D_71_PU1  {D_CAA012_71_PU1 < CLM_71_PU1 * taf_cfs * perdel_cvpmi_s }
! VA Cemetary
goal limit_D_71_PU2  {D_CAA066_71_PU2 < CLM_71_PU2 * taf_cfs * perdel_cvpmi_s }
 ! Hospital WD
goal limit_D_71_PA1  {D_DMC030_71_PA1 < CLM_71_PA1 * taf_cfs * perdel_cvpag_s } 
! West Stanislaus ID
goal limit_D_71_PA2  {D_DMC034_71_PA2 < CLM_71_PA2 * taf_cfs * perdel_cvpag_s } 


! West Stanislaus holds a post-1914 water right (A001987, L003957). Annual diversions reported tothe STate Water Board vary
! from 0-38 TAF/year (1996-2015). The water right is for a year-round diversion at a maximum rate of 262.15 cfs. The priority date is 1920.
goal limit_D_SJR082_71_PA2 {D_SJR082_71_PA2 < 262.15}
!***************************************************

! Kern Canon WD
goal limit_D_71_PA3  {D_DMC034_71_PA3 < CLM_71_PA3 * taf_cfs * perdel_cvpag_s } 
! Patterson WD
goal limit_D_71_PA4  {D_DMC044_71_PA4  < CLM_71_PA4a * taf_cfs * perdel_cvpag_s + CLM_71_PA4b * taf_cfs * perdel_cvpex_s }   !EX allocation used for water right water for 71_PA4 

! Patterson ID holds a pre-1914 water right (S009320). Annual diversions reported tothe STate Water Board vary
! from 20-62 TAF/year (1996-2015)
! A new pump station and fish screen were constructed in 2011
! The capacity of the pumping plant is 195 cfs and the lift is about 35 feet. 
goal limit_D_SJR096_71_PA4 {D_SJR096_71_PA4 < 195.}
!***************************************************

! Del Puerto, Salado, Orestimba, Sunflower
goal limit_D_71_PA5  {D_DMC044_71_PA5 < CLM_71_PA5 * taf_cfs * perdel_cvpag_s } 
! Davis/Mustang/Foothill/Quinto/Romero/Centinella
goal limit_D_71_PA6  {D_DMC064_71_PA6 < CLM_71_PA6 * taf_cfs * perdel_cvpag_s } 
! Byron/Bethany (Plainview)
goal limit_D_71_PA8  {D_DMC011_71_PA8 < CLM_71_PA8 * taf_cfs * perdel_cvpag_s } 


! For Patterson and West Stanislaus use an integer to prevent SJR diversion until all available DMC water is taken

define intSJRDiversion {INTEGER std kind 'INTEGER' UNITS 'NONE'}
define DMCShortage     {std kind 'SHORTAGE' units 'CFS'}
define DMCSupply       {std kind 'SUPPLY' units 'CFS'}

goal setDMCSupply      {DMCSupply = CLM_71_PA2 * taf_cfs * perdel_cvpag_s + CLM_71_PA4a * taf_cfs * perdel_cvpag_s + CLM_71_PA4b * taf_cfs * perdel_cvpex_s}
define DMCFullSupply   {value max(1.,CLM_71_PA2 * taf_cfs + CLM_71_PA4a * taf_cfs + CLM_71_PA4b * taf_cfs)}

goal set_DMCShortage   {DMCShortage = DMCSupply - D_DMC034_71_PA2 - D_DMC044_71_PA4}
			    
goal set_intSJRDiversion {intSJRDiversion < 1. - DMCShortage/DMCFullSupply}		    


goal setD_SJR_71_PA2_PA4 {D_SJR082_71_PA2 + D_SJR096_71_PA4 < 9999. * intSJRDiversion}


!********************************************************************************
! 71_NA1: Twin Oask IC
goal setD_SJR102_71_NA1   {D_SJR102_71_NA1 < WRmon_SJR102_71_NA1 * taf_cfs}


!********************************************************************************
! 71_NA2: El Solyo WD, NBINV AP6 LLC
goal setD_SJR090_71_NA2   {D_SJR090_71_NA2 < WRmon_SJR090_71_NA2 * taf_cfs}


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! WBA 72      !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!********************************************************************************
! 72_NA1: Riparian and Appropriative Diverters
goal setD_SJR116_72_NA1   {D_SJR116_72_NA1 < WRmon_SJR116_72_NA1 * taf_cfs}


! Laguna WD
goal limit_D_XCC025_72_PA   {D_XCC025_72_PA  < CLM_72_PA * taf_cfs * perdel_cvpag_s }
! San Luis Canal Co Exchange Contractors
goal limit_D_ARY010_72_XA1  {D_ARY010_72_XA1 < CLM_72_XA1 * taf_cfs * perdel_cvpex_s }
! Central California ID South Exchange Contractors
goal limit_D_XCC010_72_XA2  {D_XCC010_72_XA2 < CLM_72_XA2 * taf_cfs * perdel_cvpex_s }
! Central California ID North Exchange Contractors
goal limit_D_XCC054_72_XA3  {D_XCC054_72_XA3 < CLM_72_XA3 * taf_cfs * perdel_cvpex_s }

goal UseWolfsonBypass {C_XCC033 = 0.}


! Volta WA
goal limit_D_72_PR1  {DN_72_PR1 < CLM_72_PR1 * taf_cfs * perdel_cvprf_s  }
! Kesterson, Freitas, & Blue Goose units of San Luis NWR, China Island unit of North Grassland
goal limit_D_72_PR2  {DN_72_PR2 < CLM_72_PR2 * taf_cfs * perdel_cvprf_s }   
! West Bear Ck and San Luis units of San Luis NWR
goal limit_D_72_PR3  {DN_72_PR3 < CLM_72_PR3 * taf_cfs * perdel_cvprf_s }  
! Los Banos WA, Salt Slouth and Gadwall units of North Grassland
goal limit_D_72_PR4  {DN_72_PR4 < CLM_72_PR4 * taf_cfs * perdel_cvprf_s }  
! Grassland WD North 
goal limit_D_72_PR5  {DN_72_PR5 < CLM_72_PR5 * taf_cfs * perdel_cvprf_s } 
! Grassland WD South
goal limit_D_72_PR6  {DN_72_PR6 < CLM_72_PR6 * taf_cfs * perdel_cvprf_s } 

! Sonya Nechanicky of the Refuge Water Managment Program (Reclamation) reports that SLCC delivers 36% to Los Banos WA
! via the Arroya Canal. This is equivalent to 20% of the delivery to 72_PR4.
goal DN_72_PR4split1 {DN_ARY010_72_PR4 < 0.20*CLM_72_PR4 * taf_cfs * perdel_cvprf_s}
goal DN_72_PR4split2 {DN_XCC033_72_PR4 < 0.80*CLM_72_PR4 * taf_cfs * perdel_cvprf_s}

! Sonya Nechanicky of the Refuge Water Managment Program (Reclamation) reports that SLDMWA delivers 25% to Grasslands RCD
! via the Vota Wasteway. This is equivalent to 44% of the delivery to Grasslands North
goal DN_72_PR5split1 {DN_VLW008_72_PR5 < 0.44*CLM_72_PR5 * taf_cfs * perdel_cvprf_s}
goal DN_72_PR5split2 {DN_XCC054_72_PR5 < 0.56*CLM_72_PR5 * taf_cfs * perdel_cvprf_s}

! Sonya Nechanicky of the Refuge Water Managment Program (Reclamation) reports that SLCC delivers 5% to Grasslands RCD
! via the Arroyo Canal. This is equivalent to 12% of the delivery to Grasslands South
goal DN_72_PR6split1 {DN_ARY010_72_PR6 < 0.12*CLM_72_PR6 * taf_cfs * perdel_cvprf_s}
goal DN_72_PR6split2 {DN_XCC025_72_PR6 < 0.88*CLM_72_PR6 * taf_cfs * perdel_cvprf_s}

 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! WBA 73      !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Firebaugh Exchange Contractors
goal limit_D_73_XA   {D_MDOTA_73_XA + D_DMC111_73_XA < CLM_73_XA * taf_cfs * perdel_cvpex_s  }
! San Luis WD North
goal limit_D_73_PA1  {D_DMC070_73_PA1 + D_CAA087_73_PA1 < CLM_73_PA1 * taf_cfs * perdel_cvpag_s }

! From delivery data for San Luis WD 2015-2019, approx 9% is diverted from the DMC (less in dry years)
goal limit_D_73_PA1_CAA  {D_CAA087_73_PA1 < 0.91*CLM_73_PA1 * taf_cfs * perdel_cvpag_s }
goal limit_D_73_PA1_DMC  {D_DMC070_73_PA1 < 0.09*CLM_73_PA1 * taf_cfs * perdel_cvpag_s }

! Eagle Field, Mercy Springs, Oro Loma
goal limit_D_73_PA2  {D_DMC105_73_PA2 < CLM_73_PA2 * taf_cfs * perdel_cvpag_s }
! San Luis WD South, Pacheco, Panoche
goal limit_D_73_PA3  {D_DMC091_73_PA3 + D_CAA109_73_PA3 < CLM_73_PA3 * taf_cfs * perdel_cvpag_s }

! From delivery data for San Luis WD, Panoche WD, Pacheco WD 2015-2019, approx 16% is diverted from the DMC (less in dry years)
goal limit_D_73_PA3_CAA {D_CAA109_73_PA3 < 0.84*CLM_73_PA3 * taf_cfs * perdel_cvpag_s }
goal limit_D_73_PA3_DMC {D_DMC091_73_PA3 < 0.16*CLM_73_PA3 * taf_cfs * perdel_cvpag_s }

 
 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! WBA 90      !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
goal limit_D_90_PA1  {D_MDOTA_90_PA1 + D_CAA109_90_PA1 + D_CAA143_90_PA1 + D_CAA155_90_PA1 + D_CAA172_90_PA1 < CLM_90_PA1 * taf_cfs * perdel_cvpag_s }                     
goal limit_D_90_PA2  {D_CAA143_90_PA2 < CLM_90_PA2 * taf_cfs * perdel_cvpag_s }


goal limit_D_90_PU_1   {D_CAA143_90_PU < CLM_90_PUa * taf_cfs * perdel_cvpmi_s * 10.0/16.5 }
goal limit_D_90_PU_2   {D_CAA156_90_PU < CLM_90_PUa * taf_cfs * perdel_cvpmi_s * 3.0/16.5 }
goal limit_D_90_PU_3   {D_CAA165_90_PU < CLM_90_PUa * taf_cfs * perdel_cvpmi_s * 3.5/16.5 + CLM_90_PUb * taf_cfs }

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! WBA 91      !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
goal limit_D_MDOTA_91_PR    {DN_91_PR < CLM_91_PR * taf_cfs * perdel_cvprf_s  }
goal limit_D_MDOTA_91_PA    {D_MDOTA_91_PA < CLM_91_PA * taf_cfs * perdel_cvpag_s + WRM_91_PA * taf_cfs * perdel_cvpex_s }  !EX allocation used for water right water for 91_PA
                                           
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! limits to DMC transfers to XCC
! goal limit_D_DMC076_XCC043  {D_DMC076_XCC043 < CLM_72_XA3 * taf_cfs * perdel_cvpex_s }


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! San Felipe Unit     !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!                                          
goal limit_D_PCH000_SCVWD_PA    {D_PCH000_SCVWD_PA < CLM_SCVWD_PA * taf_cfs * perdel_cvpag_s }
goal limit_D_PCH000_SCVWD_PU    {D_PCH000_SCVWD_PU < CLM_SCVWD_PU * taf_cfs * perdel_cvpmi_s }
goal limit_D_PCH000_SBCWD_PA    {D_PCH000_SBCWD_PA < CLM_SBCWD_PA * taf_cfs * perdel_cvpag_s }
goal limit_D_PCH000_SBCWD_PU    {D_PCH000_SBCWD_PU < CLM_SBCWD_PU * taf_cfs * perdel_cvpmi_s }

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Kern-Pixley     !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Sonya Nechanicky (Reclamation) reports an average 25% conveyance loss WYs 2016-2020 (e-mail 01/27/21 to N. Parker)
goal limit_D_CAA239_CVPRF  {DN_D_CAA239_CVPRF_PRF < CLM_D_CAA239_CVPRF_PRF * taf_cfs * perdel_cvprf_s} !Reclamation responsible for meeting 25% conveyance loss
